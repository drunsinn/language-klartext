0  BEGIN PGM Gleichlaufnut MM
1  ;-----------Werkzeugdaten-----------
2  FN 0: Q11 =+32 ;Werkzeugnummer
3  TOOL CALL Q11 Z S5504 F5500
4  FN 18: SYSREAD Q10 = ID50 NR2 IDXQ11
5  ;--------------Nutlage--------------
6  CYCL DEF 7.0 NULLPUNKT
7  CYCL DEF 7.1  X+0
8  CYCL DEF 7.2  Y+0
9  CYCL DEF 10.0 DREHUNG
10 CYCL DEF 10.1  ROT+0
11 ;----Anfahren-der-Startposition----
12 L  X+0  Y+0 FMAX M3
13 L  Z+10 FMAX
14 ;----------Nutparameter------------
15 FN 0: Q1 =-20 ;Nuttiefe
16 FN 0: Q8 =+71 ;Nutlaenge
17 FN 0: Q6 =+12.5 ;Nutradius
18 FN 0: Q3 =+2 ;Radiale Zustellung
19 FN 0: Q4 =+5500 ;Vurschub im Gleichlauf
20 FN 0: Q5 =+15000 ;Vorschub im Leerweg
21 ;------Anfang-der-Berechnungen------
22 FN 4: Q7 =+Q3 DIV +2 ;Berechnung fuer radiale Zustellung
23 FN 2: Q2 =+Q6 - +Q10 ;Berechnung fuer WZ-Radius
24 Q9 = ( Q8 / 1 ) / Q3 ;Berechnung fuer Labelwiederholung
25 ;-----Start-der-Fraesoperation------
26 L  Z+Q1 FQ4
27 L IX+Q2 IY+0 FQ4
28 LBL 1
29 CC  X+0 IY+Q7
30 CP IPA+180 DR+ FQ4
31 L  X+Q2 R0 FQ5
32 LBL 0
33 CALL LBL 1 REPQ9
34 L  Z+100 FMAX
35 ;----Rueckstellen-der-Position-----
36 CYCL DEF 7.0 NULLPUNKT
37 CYCL DEF 7.1  X+0
38 CYCL DEF 7.2  Y+0
39 CYCL DEF 7.3  Z+0
40 CYCL DEF 10.0 DREHUNG
41 CYCL DEF 10.1  ROT+0
42 L  Z+100 FMAX M30
43 END PGM Gleichlaufnut MM
